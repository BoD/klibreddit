ext.globalProjectName = 'klibreddit'

buildscript {
    // Versions used across modules
    ext.versions = [:]
    ext.versions.with {
        gradleVersionsPlugin = '0.20.0'

        kotlin = '1.2.71'
        rxKotlin = '2.3.0'
        junit = '4.12'
        okHttp = '3.11.0'
        retrofit = '2.4.0'
        json = '20180130'
        slf4j = '1.7.25'
        gson = '2.8.5'
        wireMock = '2.19.0'
        apacheCommonsText = '1.6'
    }

    repositories {
        mavenLocal()
        google()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versions.gradleVersionsPlugin"
    }
}

allprojects {
    group = 'org.jraf'
    version = '1.0.0'

    repositories {
        mavenLocal()
        google()
        jcenter()
    }

    // Show a report in the log when running tests
    tasks.withType(Test) {
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

wrapper {
    gradleVersion = '4.10.2'
    distributionType = Wrapper.DistributionType.ALL
}

// Run './gradlew dependencyUpdates' to see new versions of dependencies
apply plugin: 'com.github.ben-manes.versions'
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { selection ->
            boolean rejected = ['alpha', 'beta', 'rc'].any { qualifier ->
                selection.candidate.version.toLowerCase() ==~ /.*-${qualifier}.*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
